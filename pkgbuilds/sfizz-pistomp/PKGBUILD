# Maintainer: pistomp
pkgname=sfizz-pistomp
pkgver=1.2.3
pkgrel=7
pkgdesc="SFZ library and LV2 plugin (minimal, headless)"
arch=('aarch64')
url="https://github.com/sfztools/sfizz-ui"
license=('BSD')
depends=('libsndfile' 'libsamplerate' 'jack2')
makedepends=('cmake' 'git' 'lv2')
provides=('sfizz' 'sfizz-lv2')
conflicts=('sfizz' 'sfizz-lv2')
source=("sfizz-ui-${pkgver}::git+https://github.com/sfztools/sfizz-ui.git#tag=${pkgver}"
        "add-mod-filetype.patch")
sha256sums=('SKIP'
            'SKIP')

prepare() {
    cd "sfizz-ui-${pkgver}"
    git submodule update --init --recursive library

    # Add MOD Audio file type annotations for SFZ file browser support
    patch -Np1 -i "${srcdir}/add-mod-filetype.patch"
}

build() {
    rm -rf build-headless
    # We turn OFF system abseil/pugixml to avoid the compilation errors
    # (arch versions are too new) but keep system sndfile and lv2.
    cmake -B build-headless -S "sfizz-ui-${pkgver}" \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release \
        -DPLUGIN_LV2=ON \
        -DPLUGIN_LV2_UI=OFF \
        -DPLUGIN_VST3=OFF \
        -DPLUGIN_PUREDATA=OFF \
        -DSFIZZ_JACK=OFF \
        -DSFIZZ_RENDER=ON \
        -DSFIZZ_USE_SYSTEM_LV2=ON \
        -DSFIZZ_USE_SYSTEM_LIBSNDFILE=ON \
        -DSFIZZ_USE_SYSTEM_ABSEIL=OFF \
        -DSFIZZ_USE_SYSTEM_PUGIXML=OFF
    cmake --build build-headless
}

package() {
    DESTDIR="${pkgdir}" cmake --install build-headless
    
    install -Dm644 "sfizz-ui-${pkgver}/library/LICENSE" \
        "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
